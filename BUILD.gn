# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

config("c++11") {
  if (current_toolchain == "//build/lib/win:msvc") {
    cflags_cc = [ "/std:c++11" ]
  } else {
    cflags_cc = [ "-std=c++11" ]
    ldflags = cflags_cc
  }
}

config("c++20") {
  if (current_toolchain == "//build/lib/win:msvc") {
    cflags_cc = [ "/std:c++20" ]
  } else {
    cflags_cc = [ "-std=c++20" ]
    ldflags = cflags_cc
  }
}

config("debug_symbols") {
  if (current_toolchain == "//build/lib/win:msvc") {
    cflags = [ "/Z7" ]
    ldflags = [ "/DEBUG:FULL" ]
  } else {
    cflags = [ "-g" ]
  }
}

config("libc++") {
  cflags_cc = [ "-stdlib=libc++" ]
  ldflags = cflags_cc
}

config("dev") {
  if (current_toolchain != "//build/lib/win:msvc") {
    cflags = [ "-O0" ]
  }
}

config("opt") {
  if (current_toolchain == "//build/lib/win:msvc") {
    cflags = [ "/Zo", "/O2", "/Oy-" ]
  } else {
    cflags = [ "-Os" ]
  }
  defines = [ "NDEBUG" ]
}

config("dbg") {
  configs = [ ":debug_symbols" ]
}

config("sanitize_memory") {
  if (current_toolchain != "//build/lib/win:msvc") {
    cflags = [
      "-fsanitize=memory",
      "-fsanitize-memory-track-origins",
    ]
    ldflags = cflags
  }
}

config("sanitize_address") {
  if (current_toolchain != "//build/lib/win:msvc") {
    cflags = [
      "-fsanitize=address",
      "-fno-omit-frame-pointer",
    ]
    ldflags = cflags
  }
}

config("sanitize_undefined") {
  if (current_toolchain != "//build/lib/win:msvc") {
    cflags = [
      "-fsanitize=undefined",
      "-fno-sanitize-recover=undefined",
    ]
    ldflags = cflags
  }
}
